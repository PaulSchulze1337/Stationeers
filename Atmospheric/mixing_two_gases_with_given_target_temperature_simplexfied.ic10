# Mixing combustion gas (HOT) ~ 2400 K with exhaust gas (EXHAUST) to a given target temperature
# Note: HOT = Input1; EXHAUST = Input2 !!!
# Note: name your pipe analyser like: HOT_PIPE, COLD_PIPE, MIXED_PIPE, EXHAUST_PIPE
# Note: This simplified version depends on a DEFINED HOT Gas from a complete combustion!
alias dial d0
alias display d1 # to show target temp
alias pipeMixer1 d2

alias hotTemp r0 # T in K
alias coldTemp r1 # T in K
alias exhaustTemp r2 # T in K
alias targetTemp r3 # T in K
alias currentRatio r4
alias remain r5 #
alias heatCapaExhaust r6 # calculated heat capacity
alias heatCapaCalulated r7 # for calculation
alias heatCapaTemp r8 # temporary value
alias exhaustEnergy r9 # temp for calculation
alias energyHot r10 # temp for calculation
alias mixRatio r11 # ratio for gas mixer
alias pressureOutput r12 # pressure on output site

define maxPressureOut 1000 # KPa = 1 MPa

define heatCapaHot 27.08 # ideal combustion gas
move heatCapaExhaust 26 # approx exhaust, highly fluctuating
define heatCapaCold 28.2  # only use CO2, min temp is 265 K!

main:
yield
# set all devices on
sbn 435685051 HASH("HOT_PIPE") On 1
sbn 435685051 HASH("COLD_PIPE") On 1
sbn 435685051 HASH("EXHAUST_PIPE") On 1
sbn 435685051 HASH("MIXED_PIPE") On 1
lbn hotTemp 435685051 HASH("HOT_PIPE") Temperature Average
lbn coldTemp 435685051 HASH("COLD_PIPE") Temperature Average
lbn exhaustTemp 435685051 HASH("EXHAUST_PIPE") Temperature Average
lbn pressureOutput 435685051 HASH("MIXED_PIPE") Pressure Average

l targetTemp dial Setting
mul targetTemp targetTemp 100 # dial with value 10 ==> 1000 K
s display Setting targetTemp

move remain 1 # reset to 100%
move heatCapaCalulated 0 # reset

lbn currentRatio 435685051 HASH("EXHAUST_PIPE") RatioCarbonDioxide Average
mul heatCapaTemp 28.2 currentRatio
add heatCapaCalulated heatCapaCalulated heatCapaTemp
sub remain remain currentRatio
lbn currentRatio 435685051 HASH("EXHAUST_PIPE") RatioNitrogen Average
mul heatCapaTemp 20.6 currentRatio
add heatCapaCalulated heatCapaCalulated heatCapaTemp
sub remain remain currentRatio
lbn currentRatio 435685051 HASH("EXHAUST_PIPE") RatioNitrousOxide Average
mul heatCapaTemp 37.2 currentRatio
add heatCapaCalulated heatCapaCalulated heatCapaTemp
sub remain remain currentRatio
lbn currentRatio 435685051 HASH("EXHAUST_PIPE") RatioOxygen Average
mul heatCapaTemp 21.1 currentRatio
add heatCapaCalulated heatCapaCalulated heatCapaTemp
sub remain remain currentRatio
lbn currentRatio 435685051 HASH("EXHAUST_PIPE") RatioPollutant Average
mul heatCapaTemp 24.8 currentRatio
add heatCapaCalulated heatCapaCalulated heatCapaTemp
sub remain remain currentRatio

mul heatCapaTemp heatCapaExhaust remain
add heatCapaCalulated heatCapaCalulated heatCapaTemp # Final value

# energy_hot = heatCapa * (T_hot - T_out) * T_cold
sub exhaustEnergy exhaustTemp targetTemp
mul exhaustEnergy heatCapaCalulated exhaustEnergy
mul exhaustEnergy exhaustEnergy hotTemp #
# energy_cold = heatCapa * (T_out - T_cold) * T_Hot
sub energyHot targetTemp hotTemp
mul energyHot heatCapaCold energyHot
mul energyHot energyHot exhaustTemp #
# mixRatio = energyCold / (energyHot-energyCold)
add mixRatio exhaustEnergy energyHot
div mixRatio energyHot mixRatio
mul mixRatio mixRatio 100 # make it %
# IMPORTANT invert mixRatio to 100 - mixRatio
sub mixRatio 100 mixRatio
s pipeMixer1 Setting mixRatio
s db Setting mixRatio

bge exhaustTemp hotTemp setMixerTotalOff # a>=b
bge targetTemp hotTemp setMixerTotalOff # a>=b
blt targetTemp exhaustTemp setMixerTotalOff # a<b
bgt pressureOutput maxPressureOut setMixerOff # a>b

s pipeMixer1 On 1 # turn off
j main

setMixerTotalOff:
s pipeMixer1 On 0 # turn off
s display Setting 404 # show error
j main

setMixerOff:
s pipeMixer1 On 0 # turn off
j main

