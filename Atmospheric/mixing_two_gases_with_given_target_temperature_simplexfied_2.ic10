# Mixing two different gases to a given target temp
# Note: name your pipe analyser like: HOT_PIPE, COLD_PIPE, MIXED_PIPE, EXHAUST_PIPE
# Note: This simplified version depends on a DEFINED HOT Gas from a complete combustion!
# Note: Works only with a given HOT and COLD gas!
alias dial d0
alias display d1 # to show target temp
alias pipeMixer1 d2

alias hotTemp r0 # T in K
alias coldTemp r1 # T in K
alias exhaustTemp r2 # T in K
alias targetTemp r3 # T in K
alias currentRatio r4
alias remainingRatio r5 #
alias heatCapaExhaust r6 # calculated heat capacity
alias heatCapaCalulated r7 # for calculation
alias heatCapaTemp r8 # temporary value
alias energyHot r9 # temp for calculation
alias energyCold r10 # temp for calculation
alias mixRatio r11 # ratio for gas mixer
alias pressureOutput r12 # pressure on output site

define maxPressureOut 1000 # KPa = 1 MPa

define heatCapaHot 27.08 # ideal combustion gas
move heatCapaExhaust 26 # approx exhaust, highly fluctuating
define heatCapaCold 28.2  # only use CO2, min temp is 265 K!

main:
yield
# set all devices on
sbn 435685051 HASH("HOT_PIPE") On 1
sbn 435685051 HASH("COLD_PIPE") On 1
sbn 435685051 HASH("EXHAUST_PIPE") On 1
sbn 435685051 HASH("MIXED_PIPE") On 1
lbn hotTemp 435685051 HASH("HOT_PIPE") Temperature Average
lbn coldTemp 435685051 HASH("COLD_PIPE") Temperature Average
lbn exhaustTemp 435685051 HASH("EXHAUST_PIPE") Temperature Average
lbn pressureOutput 435685051 HASH("MIXED_PIPE") Pressure Average

l targetTemp dial Setting
mul targetTemp targetTemp 100 # dial with value 10 ==> 1000 K
s display Setting targetTemp

move remainingRatio 1 # reset
move heatCapaCalulated 0 # reset

lbn currentRatio 435685051 HASH("EXHAUST_PIPE") RatioCarbonDioxide Average
mul heatCapaTemp 28.2 currentRatio
add heatCapaCalulated heatCapaCalulated heatCapaTemp
sub remainingRatio remainingRatio currentRatio

lbn currentRatio 435685051 HASH("EXHAUST_PIPE") RatioNitrogen Average
mul heatCapaTemp 20.6 currentRatio
add heatCapaCalulated heatCapaCalulated heatCapaTemp
sub remainingRatio remainingRatio currentRatio

lbn currentRatio 435685051 HASH("EXHAUST_PIPE") RatioNitrousOxide Average
mul heatCapaTemp 37.2 currentRatio
add heatCapaCalulated heatCapaCalulated heatCapaTemp
sub remainingRatio remainingRatio currentRatio

lbn currentRatio 435685051 HASH("EXHAUST_PIPE") RatioOxygen Average
mul heatCapaTemp 21.1 currentRatio
add heatCapaCalulated heatCapaCalulated heatCapaTemp
sub remainingRatio remainingRatio currentRatio

lbn currentRatio 435685051 HASH("EXHAUST_PIPE") RatioPollutant Average
mul heatCapaTemp 24.8 currentRatio
add heatCapaCalulated heatCapaCalulated heatCapaTemp
sub remainingRatio remainingRatio currentRatio

mul heatCapaTemp heatCapaExhaust remainingRatio
add heatCapaCalulated heatCapaCalulated heatCapaTemp # Final value

# energy_hot = heatCapa * (T_hot - T_out) * T_cold
sub energyHot exhaustTemp targetTemp
mul energyHot heatCapaCalulated energyHot
mul energyHot energyHot coldTemp #
# energy_cold = heatCapa * (T_out - T_cold) * T_Hot
sub energyCold targetTemp coldTemp
mul energyCold heatCapaCold energyCold
mul energyCold energyCold exhaustTemp #
# mixRatio = energyCold / (energyHot-energyCold)
add mixRatio energyHot energyCold
div mixRatio energyCold mixRatio
mul mixRatio mixRatio 100 # make it %

s db Setting mixRatio

bge coldTemp hotTemp setMixerTotalOff # coldT > hotT, turn off mixer
bge targetTemp hotTemp setMixerTotalOff # out of range, turn off mixer
blt targetTemp coldTemp setMixerTotalOff # out of range, turn off mixer
bgt pressureOutput maxPressureOut setMixerOff # pressure too high, turn off mixer

s pipeMixer1 Setting mixRatio
s pipeMixer1 On 1 # turn off
j main

setMixerTotalOff:
s pipeMixer1 On 0 # turn off
s pipeMixer1 Setting 0
s display Setting 404 # show error
s db Setting 404 # show error
j main

setMixerOff:
s pipeMixer1 On 0 # turn off
j main

