# Mixing two different gases to a given target temp
# Note: Works only with a given HOT and COLD gas!
alias pipeHot d0 # pipeAnalyser on Hot site
alias pipeCold d1 # pipeAnalyser on Cold site
alias pipeMixer d2
alias dial d3 #  to set target temp
alias display d4 # to show target temp
alias pipeAnalyerMix d5 # pipeAnalyser on Mix site

alias hotTemp r0 # T in K
alias coldTemp r1 # T in K
alias targetTemp r2 # T in K
alias heatCapaHot r3 # heat capacity hot [J/mol*K]
alias heatCapaCold r4 # heat capacity cold [J/Mol*K]
alias curHeatCapa r5 # temp for calculation
alias curRatio r6 # temp for calculation
alias calcHeatCapa r7 # calculated heat capacity
alias energyHot r8 # energy content [J/mol]
alias energyCold r9 # energy content [J/mol]
alias curTemp r10 # temp for calcuclations
alias curEnergy r11 # temp for calculations
alias mixRatio r12 # ratio for gas mixer
alias dialValue r13 # for target temp
alias pressureOutput r14 # pressure on output site

define heatCapaCO2 28.2 # heat capacity of CO2
define heatCapaH2 20.4 # .. H2
define heatCapaN2 20.6 # .. N2
define heatCapaN2O 37.2 # .. N2O
define heatCapaO2 21.1 # ..O2
define heatCapaX 24.8 # .. Polutants
define maxPressureOut 1000 # KPa = 1 MPa

main:
yield
s pipeHot On 1 # allways on
s pipeCold On 1 # allways on
s pipeAnalyerMix On 1 # allways on
l hotTemp pipeHot Temperature # get Hot site temp
l coldTemp pipeCold Temperature # get cold temp
l dialValue dial Setting # get target temp
mul targetTemp dialValue 100
bge coldTemp hotTemp setMixerTotalOff
bge targetTemp hotTemp setMixerTotalOff
blt targetTemp coldTemp setMixerTotalOff
s display Setting targetTemp
l pressureOutput pipeAnalyerMix Pressure
bgt pressureOutput maxPressureOut setMixerOff
move calcHeatCapa 0 # reset for next loop
# calulacte heat capacity values for hot and cold
l curRatio pipeHot RatioCarbonDioxide
move curHeatCapa heatCapaCO2
jal calcHeatCapacity #exec and save return address
l curRatio pipeHot RatioVolatiles
move curHeatCapa heatCapaH2
jal calcHeatCapacity
l curRatio pipeHot RatioNitrogen
move curHeatCapa heatCapaN2
jal calcHeatCapacity
l curRatio pipeHot RatioNitrousOxide
move curHeatCapa heatCapaN2O
jal calcHeatCapacity
l curRatio pipeHot RatioOxygen
move curHeatCapa heatCapaO2
jal calcHeatCapacity
l curRatio pipeHot RatioPollutant
move curHeatCapa heatCapaX
jal calcHeatCapacity
move heatCapaHot calcHeatCapa
move calcHeatCapa 0 # reset for next calculation
l curRatio pipeCold RatioCarbonDioxide
move curHeatCapa heatCapaCO2
jal calcHeatCapacity
l curRatio pipeCold RatioVolatiles
move curHeatCapa heatCapaH2
jal calcHeatCapacity
l curRatio pipeCold RatioNitrogen
move curHeatCapa heatCapaN2
jal calcHeatCapacity
l curRatio pipeCold RatioNitrousOxide
move curHeatCapa heatCapaN2O
jal calcHeatCapacity
l curRatio pipeCold RatioOxygen
move curHeatCapa heatCapaO2
jal calcHeatCapacity
l curRatio pipeCold RatioPollutant
move curHeatCapa heatCapaX
jal calcHeatCapacity
move heatCapaCold calcHeatCapa
# energy_hot = heatCapa * (T_hot - T_out) * T_cold
sub curTemp hotTemp targetTemp
mul energyHot heatCapaHot curTemp
mul energyHot energyHot coldTemp
# energy_cold = heatCapa * (T_out - T_cold) * T_Hot
sub curTemp targetTemp coldTemp
mul energyCold heatCapaCold curTemp
mul energyCold energyCold hotTemp
# mixRatio = energyCold / (energyHot-energyCold)
add curEnergy energyHot energyCold
div mixRatio energyCold curEnergy
mul mixRatio mixRatio 100 # make it %
round mixRatio mixRatio
s pipeMixer Setting mixRatio
s pipeMixer On 1 # turn on
s db Setting mixRatio
j main

calcHeatCapacity:
# partialHeatCapa = ration_cas * heatCapacity_gas
mul curHeatCapa curHeatCapa curRatio
add calcHeatCapa calcHeatCapa curHeatCapa
j ra # jump back return adress

setMixerTotalOff:
s pipeMixer On 0 # turn mixer off
s pipeMixer Setting 0
s display Setting 404 # show error on display
s db Setting 404 # show error in IC Housing
j main

setMixerOff:
s pipeMixer On 0 # turn mixer off
#s db Setting 303
s db Setting pressureOutput
j main