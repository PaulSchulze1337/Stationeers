##########################################################################################
# YOU HAVE TO REMOVE ALL COMMENTS TO FIT THIS SNIPPET INTO THE IC10!
# Minimizer: https://github.com/PaulSchulze1337/stationeers-ic10-code-minimizer
##########################################################################################
# MIXING THREE GASES TO A GIVEN TARGET TEMPERATURE. THIS SCRIPT DEPENGS ON DEFINED GASES!
# - HOT Gas (combustion gas!) ~ 2400 K
# - EXHAUST Gas (can very in components and temperature) ~ 300 K
# - COLD Gas (CO2!) ~ 300 K or higher
#
# - Name your pipe analyser like: HOT_PIPE, COLD_PIPE, MIXED_PIPE, EXHAUST_PIPE
#
#  You may need to adjust the mixing ratio by sub 100, or uncomment it below.
#  MIXER_HOT_EXHAUST = Input1 (HOT), Input2 (EXHAUST)
#  MIXER_COLD_EXHAUST = Input1 (EXHAUST), Input2 (COLD)
#
#  Connect the MIXED_PIPE to a furnace and both MIXER to this mixed pipe.
#  Connect a HOT_PIPE and a EXHAUST_PIPE to the MIXER_HOT_EXHAUST.
#  Connect a COLD_PIPE and a EXHAUST_PIPE to the MIXER_COLD_EXHAUST.
##########################################################################################
alias dial d0
alias display d1 # to show target temp
alias mixHotExhaust d2 # (HOT + EXHAUST)
alias mixColdExhaust d3 # (COLD + EXHAUST)

alias hotTemp r0 # T in K
alias coldTemp r1 # T in K
alias exhaustTemp r2 # T in K
alias targetTemp r3 # T in K
alias currentRatio r4
alias remain r5 #
alias heatCapaExhaust r6 # calculated heat capacity
alias heatCapaCalulated r7 # for calculation
alias heatCapaTemp r8 # temporary value

alias exhaustEnergy r9 # temp for calculation
alias energySecondGas r10 # temp for calculation
alias mixRatio r11 # ratio for gas mixer

alias secondGasTemp r12
alias secondGasCapa r13

alias pressureOutput r14 # pressure on output site
define maxPressureOut 1000 # KPa = 1 MPa

define heatCapaHot 27.08 # ideal combustion gas
move heatCapaExhaust 26 # approx exhaust, highly fluctuating
define heatCapaCold 28.2  # only use CO2, min temp is 265 K!

main:
yield
# set all devices on
sbn 435685051 HASH("HOT_PIPE") On 1
sbn 435685051 HASH("COLD_PIPE") On 1
sbn 435685051 HASH("EXHAUST_PIPE") On 1
sbn 435685051 HASH("MIXED_PIPE") On 1
lbn hotTemp 435685051 HASH("HOT_PIPE") Temperature Average
lbn coldTemp 435685051 HASH("COLD_PIPE") Temperature Average
lbn exhaustTemp 435685051 HASH("EXHAUST_PIPE") Temperature Average
lbn pressureOutput 435685051 HASH("MIXED_PIPE") Pressure Average

l targetTemp dial Setting
mul targetTemp targetTemp 100 # dial with value 10 ==> 1000 K
s display Setting targetTemp

# Frist check if we are able to mix the gases
blt targetTemp coldTemp setMixerTotalOff # a<b
bgt targetTemp hotTemp setMixerTotalOff # a>b

move remain 1 # reset to 100%
move heatCapaCalulated 0 # reset

lbn currentRatio 435685051 HASH("EXHAUST_PIPE") RatioCarbonDioxide Average
mul heatCapaTemp 28.2 currentRatio
add heatCapaCalulated heatCapaCalulated heatCapaTemp
sub remain remain currentRatio
lbn currentRatio 435685051 HASH("EXHAUST_PIPE") RatioNitrogen Average
mul heatCapaTemp 20.6 currentRatio
add heatCapaCalulated heatCapaCalulated heatCapaTemp
sub remain remain currentRatio
lbn currentRatio 435685051 HASH("EXHAUST_PIPE") RatioNitrousOxide Average
mul heatCapaTemp 37.2 currentRatio
add heatCapaCalulated heatCapaCalulated heatCapaTemp
sub remain remain currentRatio
lbn currentRatio 435685051 HASH("EXHAUST_PIPE") RatioOxygen Average
mul heatCapaTemp 21.1 currentRatio
add heatCapaCalulated heatCapaCalulated heatCapaTemp
sub remain remain currentRatio
lbn currentRatio 435685051 HASH("EXHAUST_PIPE") RatioPollutant Average
mul heatCapaTemp 24.8 currentRatio
add heatCapaCalulated heatCapaCalulated heatCapaTemp
sub remain remain currentRatio

mul heatCapaTemp heatCapaExhaust remain
add heatCapaCalulated heatCapaCalulated heatCapaTemp # Final value

bltal targetTemp exhaustTemp calcWithCold # a<b EXHAUST+COLD
bgeal targetTemp exhaustTemp calcWithHot # a>=b EXHAUST+HOT

# energy_hot = heatCapa * (T_hot - T_out) * T_cold
sub exhaustEnergy exhaustTemp targetTemp
mul exhaustEnergy heatCapaCalulated exhaustEnergy
mul exhaustEnergy exhaustEnergy secondGasTemp #

# energy_cold = heatCapa * (T_out - T_cold) * T_Hot
sub energySecondGas targetTemp secondGasTemp
mul energySecondGas secondGasCapa energySecondGas
mul energySecondGas energySecondGas exhaustTemp #
# mixRatio = energyCold / (energyHot-energyCold)
add mixRatio exhaustEnergy energySecondGas
div mixRatio energySecondGas mixRatio
mul mixRatio mixRatio 100 # make it %
# IMPORTANT: INPUT1 = EXHAUST, INPUT2 = HOT

s db Setting mixRatio
bgt pressureOutput maxPressureOut setMixerOff # a>b
bgeal targetTemp exhaustTemp setMixExhaustWHot # a>=b
bltal targetTemp exhaustTemp setMixExhaustWCold # a<b EXHAUST+COLD
# just to be sure
j main

setMixerTotalOff:
s mixHotExhaust On 0 # turn off
s mixColdExhaust On 0 # turn off
s display Setting 404 # show error
j main

setMixerOff:
s mixHotExhaust On 0 # turn off
s mixColdExhaust On 0 # turn off
j main

calcWithHot:
move secondGasTemp hotTemp
move secondGasCapa heatCapaHot
j ra

calcWithCold:
move secondGasTemp coldTemp
move secondGasCapa heatCapaCold
j ra

setMixExhaustWHot:
sub mixRatio 100 mixRatio
s mixHotExhaust Setting mixRatio
s mixHotExhaust On 1 # turn on
j main

setMixExhaustWCold:
s mixColdExhaust Setting mixRatio
s mixColdExhaust On 1 # turn on
j main

