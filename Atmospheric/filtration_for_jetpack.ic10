##########################################################################################
# This Script is used to control a filtration unit.
# We only want to filter the air if the pressure is above a certain pressure threshold.
# as well as cut of filtering if the filters are nearly empty.
# Note: Gas sensor measures in kPa!!!
##########################################################################################
alias filterUnit db # db refers to the filtration unit
alias tempSensor d0
alias activeVent d1
alias atmophericTemp r0
alias inputPressure r1
alias filter0Remains r2
alias filter1Remains r3
alias outputPipePressure r4 # pressure of the output pipe
#
define MINPRESSURE 10000 # 10 MPa
define MAXPRESSURE 45000 # 45 MPa
##########################################################################################

main:
yield # wait for 1 tick
l atmophericTemp tempSensor Temperature # read temp
bltal atmophericTemp 270 activeVentOff #  temp < 270 K
bgeal atmophericTemp 270 activeVentOn #   temp >= 270 K

l inputPressure filterUnit PressureInput
ls filter0Remains filterUnit 0 Quantity # 0 = Gas Filter A
ls filter1Remains filterUnit 1 Quantity # 1 = Gas Filter B

### cut of filtering if the filters are nearly empty
bleal filter0Remains 5 stopFilteringCutOff
bleal filter1Remains 5 stopFilteringCutOff

l outputPipePressure filterUnit PressureOutput # read pressure of the output pipe
bleal inputPressure MINPRESSURE stopFilteringCutOff # a <= b
bltal outputPipePressure MAXPRESSURE startFiltering # a < b
bgeal outputPipePressure MAXPRESSURE stopFiltering  # a >= b
j main


activeVentOn:
s activeVent PressureInternal 45000 # set pressure to 45 MPa
s activeVent On 1 # turn on the active vent
j ra

activeVentOff:
s activeVent On 0 # turn off the active vent
j ra

stopFilteringCutOff:
s filterUnit Mode 0
j main

stopFiltering:
s filterUnit Mode 0
j ra

startFiltering:
s filterUnit Mode 1
j ra