# Mixing two different gases to a given target temp
# Note: Works only with a given HOT and COLD gas!
alias pipeHot d0 # pipeAnalyser on Hot site
alias pipeCold d1 # pipeAnalyser on Cold site
alias pipeMixer d2
alias dial d3 #  to set target temp
alias display d4 # to show target temp

alias hotTemp r0 # Temp in Kelvin
alias coldTemp r1 # Temp in Kelvin
alias targetTemp r2 # Temp in Kelvin
alias heatCapaHot r3 # heat capacity hot [J/mol*K]
alias heatCapaCold r4 # heat capacity cold [J/Mol*K]
alias curHeatCapa r5 # temp for calculation
alias curRatio r6    # temp for calculation
alias calcHeatCapa r7 # calculated heat capacity
alias energyHot r8 # energy content [J/mol]
alias energyCold r9 # energy content [J/mol]
alias curTemp r10 # temp for calcuclations
alias curEnergy r11 # temp for calculations
alias mixRatio r12 # ratio for gas mixer
alias dialValue r13 # for target temp

define heatCapaCO2 28.2 # heat capacity of CO2
define heatCapaH2 20.4 # heat capacity of H2
define heatCapaN2 20.6 # heat capacity of N2
define heatCapaN2O 23.0 # heat capacity of N2O
define heatCapaO2 21.1 # heat capacity of O2
define heatCapaX 24.8 # heat capacity of Polutants

main:
yield # pause for 0.5 sec
l hotTemp pipeHot Temperature # get Hot site temp
l coldTemp pipeCold Temperature # get cold temp
l dialValue dial Setting # get target temp from dial
mul targetTemp dialValue 100
bge coldTemp hotTemp setMixerOff # safe guard
bge targetTemp hotTemp setMixerOff # save guard
blt targetTemp coldTemp setMixerOff # save guard
s display Setting targetTemp
move calcHeatCapa 0 # reset for next loop
# calulacte heat capacity values for hot and cold
l curRatio pipeHot RatioCarbonDioxide
move curHeatCapa heatCapaCO2
jal calcHeatCapacity #exec and save return address
l curRatio pipeHot RatioVolatiles
move curHeatCapa heatCapaH2
jal calcHeatCapacity #exec and save return address
l curRatio pipeHot RatioNitrogen
move curHeatCapa heatCapaN2
jal calcHeatCapacity #exec and save return address
l curRatio pipeHot RatioNitrousOxide
move curHeatCapa heatCapaN2O
jal calcHeatCapacity #exec and save return address
l curRatio pipeHot RatioOxygen
move curHeatCapa heatCapaO2
jal calcHeatCapacity #exec and save return address
l curRatio pipeHot RatioPollutant
move curHeatCapa heatCapaX
jal calcHeatCapacity #exec and save return address
move heatCapaHot calcHeatCapa
move calcHeatCapa 0 # reset for next calculation
l curRatio pipeCold RatioCarbonDioxide
move curHeatCapa heatCapaCO2
jal calcHeatCapacity #exec and save return address
l curRatio pipeCold RatioVolatiles
move curHeatCapa heatCapaH2
jal calcHeatCapacity #exec and save return address
l curRatio pipeCold RatioNitrogen
move curHeatCapa heatCapaN2
jal calcHeatCapacity #exec and save return address
l curRatio pipeCold RatioNitrousOxide
move curHeatCapa heatCapaN2O
jal calcHeatCapacity #exec and save return address
l curRatio pipeCold RatioOxygen
move curHeatCapa heatCapaO2
jal calcHeatCapacity #exec and save return address
l curRatio pipeCold RatioPollutant
move curHeatCapa heatCapaX
jal calcHeatCapacity #exec and save return address
move heatCapaCold calcHeatCapa
# energy_hot = heatCapa * (T_hot - T_out) * T_cold
sub curTemp hotTemp targetTemp
mul energyHot heatCapaHot curTemp
mul energyHot energyHot coldTemp
# energy_cold = heatCapa * (T_out - T_cold) * T_Hot
sub curTemp targetTemp coldTemp
mul energyCold heatCapaCold curTemp
mul energyCold energyCold hotTemp
# mixRatio = energyCold / (energyHot-energyCold)
add curEnergy energyHot energyCold
div mixRatio energyCold curEnergy
mul mixRatio mixRatio 100 # make ratio percent
round mixRatio mixRatio
s pipeMixer Setting mixRatio
s db Setting mixRatio
j main

calcHeatCapacity:
# partialHeatCapa = ration_cas * heatCapacity_gas
mul curHeatCapa curHeatCapa curRatio
add calcHeatCapa calcHeatCapa curHeatCapa
j ra # jump back return adress

setMixerOff:
s pipeMixer On 0 # turn off mixer
s pipeMixer Setting 0
s display Setting 404 # show error on display
s db Setting 404 # show error in IC Housing
j main